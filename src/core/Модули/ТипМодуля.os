Перем ОбщийМодуль Экспорт;

Перем МодульОбъекта Экспорт;
Перем МодульМенеджера Экспорт;

Перем МодульОбычнойФормы Экспорт;
Перем МодульУправляемойФормы Экспорт;

Перем МодульКоманды Экспорт;

Перем МодульСеанса Экспорт;
Перем МодульОбычногоПриложения Экспорт;
Перем МодульУправляемогоПриложения Экспорт;
Перем МодульВнешнегоСоединения Экспорт;

///////////////////////////////////////////////////////////////////////////////////////////////

Функция ТипМодуляПоИмениФайла(ПолноеИмяФайла)Экспорт

	Файл = Новый Файл(ПолноеИмяФайла);
    ИмяБезРасширения = Файл.ИмяБезРасширения;
    Расширение = Файл.Расширение;
    
    ТипЭтогоМодуля = "";

    Если ИмяБезРасширения = "CommandModule" Тогда

		ТипЭтогоМодуля = МодульКоманды;

    ИначеЕсли ИмяБезРасширения = "ManagedApplicationModule" Тогда

		ТипЭтогоМодуля = МодульУправляемогоПриложения;

    ИначеЕсли ИмяБезРасширения = "OrdinaryApplicationModule" Тогда

		ТипЭтогоМодуля = МодульОбычногоПриложения;    

    ИначеЕсли ИмяБезРасширения = "SessionModule" Тогда

		ТипЭтогоМодуля = МодульСеанса;

    ИначеЕсли ИмяБезРасширения = "ExternalConnectionModule" Тогда

        ТипЭтогоМодуля = МодульВнешнегоСоединения;

    ИначеЕсли ИмяБезРасширения = "ManagerModule"
        	ИЛИ ИмяБезРасширения = "ValueManagerModule" Тогда

		ТипЭтогоМодуля = МодульМенеджера;

    ИначеЕсли ИмяБезРасширения = "ObjectModule"
        	ИЛИ ИмяБезРасширения = "RecordSetModule" Тогда

		ТипЭтогоМодуля = МодульОбъекта;

	ИначеЕсли ИмяБезРасширения = "module" И ПустаяСтрока(Расширение) Тогда

        ТипЭтогоМодуля = МодульОбычнойФормы;

    ИначеЕсли ИмяБезРасширения = "Module" Тогда

		МассивЧастейИмени = СтрРазделить(ПолноеИмяФайла, "\");
        Если МассивЧастейИмени.Количество() > 3 Тогда
			
			Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - 2];
            Если Родитель = "Ext" Тогда

				Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - 4];
                Если Родитель = "CommonModules" Тогда

                    ТипЭтогоМодуля = ОбщийМодуль;

                ИначеЕсли Родитель = "WebServices" Тогда

                    ТипЭтогоМодуля = МодульОбъекта;

                КонецЕсли;
				
            ИначеЕсли Родитель = "Form" Тогда

				ТипЭтогоМодуля = МодульУправляемойФормы;

            Иначе

                Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - 3];
                Если Родитель = "CommonModule" Тогда

                    ТипЭтогоМодуля = ОбщийМодуль;

                ИначеЕсли Родитель = "WebService" Тогда

                    ТипЭтогоМодуля = МодульОбъекта;

                КонецЕсли;

            КонецЕсли;		

		Иначе

            ВызватьИсключение "Ошибочная структура имени файла: " + ПолноеИмяФайла;

        КонецЕсли; 
		
    КонецЕсли;

	Возврат ТипЭтогоМодуля;

КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////

ОбщийМодуль = "ОбщийМодуль";

МодульОбъекта = "МодульОбъекта";
МодульМенеджера = "МодульМенеджера";

МодульОбычнойФормы = "МодульОбычнойФормы";
МодульУправляемойФормы = "МодульУправляемойФормы";

МодульКоманды = "МодульКоманды";

МодульСеанса = "МодульСеанса";
МодульОбычногоПриложения = "МодульОбычногоПриложения";
МодульУправляемогоПриложения = "МодульУправляемогоПриложения";
МодульВнешнегоСоединения = "МодульВнешнегоСоединения";